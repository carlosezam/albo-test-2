plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'jacoco'
    id 'project-report'
}

jacoco {
    toolVersion '0.8.7'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def fileFilter = [
        // data binding
        'android/databinding/**/*.class', '**/android/databinding/*Binding.class', '**/android/databinding/*', '**/androidx/databinding/*', '**/BR.*',
        '**/Fragment*Binding.class','**/databinding/*Binding.class',
        // android
        '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
        // kotlin
        '**/*MapperImpl*.*','**/*$ViewInjector*.*','**/*$ViewBinder*.*','**/BuildConfig.*','**/*Component*.*','**/*BR*.*','**/Manifest*.*',
        '**/*$Lambda$*.*','**/*Companion*.*','**/*Module*.*','**/*Dagger*.*','**/*Hilt*.*','**/*MembersInjector*.*','**/*_MembersInjector.class',
        '**/*_Factory*.*', '**/*_Provide*Factory*.*', '**/*Extensions*.*',
        '**/*$Lambda*.*','**/*New Function*.*',
        // sealed and data classes
        '**/*$Result.*', '**/*$Result$*.*',
        // adapters generated by moshi
        '**/*JsonAdapter.*',
        // room
        '**/*_Impl*.*', '**/*$suspendImpl*'
]

task reportDebugTestCoverage( type: JacocoReport) {
    dependsOn = ['testDebugUnitTest', 'createDebugCoverageReport']
    group 'reporting'
    description 'Generate Jacoco coverage reports after running testDebugUnitTests'

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = true
    }

    print( buildDir )
    def sourceName = 'debug'
    def javaTree = fileTree(dir: "${buildDir}/intermediates/javac/$sourceName/classes", excludes: fileFilter)
    def kotlinTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/$sourceName", excludes: fileFilter)
    classDirectories.from = files([javaTree], [kotlinTree])

    println(buildDir)
    def unitTestData = ["outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec"]
    def androidTestData = ["outputs/code_coverage/debugAndroidTest/connected/*.ec"]
    executionData.from = fileTree(dir: buildDir, includes: unitTestData + androidTestData)
    sourceDirectories.from = files( ["src/main/java"] )
    additionalSourceDirs.from = files( ["src/main/java"] )

}
android {

    // load local.properties
    Properties properties =new Properties()
    if( rootProject.file("local.properties").exists() ) {
        properties.load( rootProject.file("local.properties").newDataInputStream() )
    }

    signingConfigs {
        release {
            if( getPropOrEnv("STORE_FILE", properties) ) {
                storeFile file( getPropOrEnv("STORE_FILE", properties) )
            }
            storePassword getPropOrEnv("STORE_PASSWORD", properties)
            keyAlias getPropOrEnv("KEY_ALIAS", properties)
            keyPassword getPropOrEnv("KEY_PASSWORD", properties)
        }
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.carlosezam.punky"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 6
        versionName "5.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true
        unitTests {

            includeAndroidResources true
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        // for JNA and JNA-platform
        /*exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
        // for byte-buddy
        exclude "META-INF/licenses/ASM"
        pickFirst "win32-x86-64/attach_hotspot_windows.dll"
        pickFirst "win32-x86/attach_hotspot_windows.dll" */
    }
}

dependencies {

    //implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // testing
    implementation 'io.github.serpro69:kotlin-faker:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version") {
        // see: https://github.com/Kotlin/kotlinx.coroutines/tree/master/kotlinx-coroutines-debug#debug-agent-and-android
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }

    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"

    // ktor
    //implementation "io.ktor:ktor-client-android:$ktor_version"
    //implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.2"



    // kotlinx serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1"

    // ktx
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
}

static def getPropOrEnv(String entry, Properties properties = null){
    return properties?.getProperty(entry, "") ?: System.getenv( entry )
}